import java.util.stream.Collectors

plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
}

group 'de.nocoffeetech'
version '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

ext {
    javaMainClass = "de.nocoffeetech.webservices.launcher.Launcher"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { //for brigadier
        url "https://libraries.minecraft.net"
    }
}

sourceSets {
    launcher {
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

compileJava {
    doFirst {
        options.compilerArgs += [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.apache.logging.log4j') {
            details.useVersion "2.20.0"
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    api group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'
    api group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    api project(":")

    implementation 'net.minecrell:terminalconsoleappender:1.3.0'
    api 'com.mojang:brigadier:1.0.18'

    runtimeOnly group: 'com.aayushatharva.brotli4j', name: 'native-linux-x86_64', version: '1.12.0'
    implementation group: 'com.aayushatharva.brotli4j', name: 'brotli4j', version: '1.12.0'
}

test {
    useJUnitPlatform()
}

tasks.register('generatePackage', Copy) {
    //from 'config.json.example'
    into "${buildDir}/release/"
}

// Configure the custom task to copy specific files to the release folder
generatePackage {
    includeEmptyDirs = false
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from('./example') {
        include 'config.json.example'
        rename { 'config.json' }
        into '.'
    }

    from('./example/test') {
        include '*'
        into './test'
    }

    from("${buildDir}/libs") {
        include 'webservices-launcher.jar'
        into '.'
    }

    // Copy other files from libs to the plugins subfolder of the release folder
    from("${buildDir}/libs") {
        exclude 'webservices-launcher.jar'
        into './plugins/'
    }

    // Copy the deps folder from the build directory to the libs subfolder of the release folder
    from(sourceSets.main.runtimeClasspath) {
        include('*.jar')
        includeEmptyDirs = false
        into './libs/'
    }
}

// Add a dependency on the custom task to ensure it runs after the build
generatePackage.dependsOn jar
generatePackage.dependsOn 'launcherJar'
build.dependsOn generatePackage

tasks.register('launcherJar', Jar) {
    from(sourceSets.launcher.output)
    archiveFileName = 'webservices-launcher.jar'
    manifest {
        attributes("Main-Class": javaMainClass)
    }
}

tasks.register('run', JavaExec) {
    classpath = sourceSets.launcher.runtimeClasspath
    mainClass = javaMainClass
    jvmArgs = ["-Dwebservices.dev=true", "-Dwebservices.launcher.additional_modules=" + sourceSets.main.runtimeClasspath.toList().stream().map(Object::toString).collect(Collectors.joining(File.pathSeparator))]
}

build.dependsOn(launcherJar)

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allJava
}

tasks.register('launcherSourceJar', Jar) {
    from sourceSets.launcher.allJava
}

publishing {
    publications {
        launcher(MavenPublication) {
            artifact launcherJar {
                artifactId "webservices-launcher"
            }
            artifact launcherSourceJar {
                artifactId "webservices-launcher"
                classifier "sources"
            }
        }
        services(MavenPublication) {
            from components.java
            artifact sourceJar {
                artifactId "webservices-core"
                classifier "sources"
            }
        }
    }
}
publishLauncherPublicationToMavenLocal.dependsOn(sourceJar)
publishServicesPublicationToMavenLocal.dependsOn(launcherSourceJar)

